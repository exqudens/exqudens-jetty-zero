plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "4.0.1"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task sourcesJar(type: Jar, dependsOn: ['classes']) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: ['javadoc']) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives jar
    archives shadowJar
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

jar {
    manifest {
        attributes 'Project-Group': project.group, 'Project-Name': project.name, 'Project-Version': project.version
    }
}

shadowJar {
    classifier = 'all'
    mergeServiceFiles()
    manifest {
        attributes(
                'Project-Group': project.group,
                'Project-Name': project.name,
                'Project-Version': project.version,
                'Main-Class': 'exqudens.jetty.Application'
        )
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    //compileOnly group: "org.projectlombok", name: "lombok", version: "1.16.18"

    //compile group: "org.slf4j", name: "slf4j-api", version: "1.7.21"

    //runtime group: "ch.qos.logback", name: "logback-classic", version: "1.1.7"

    compile group: "org.eclipse.jetty", name: "jetty-server",  version: "9.2.11.v20150529"
    compile group: "org.eclipse.jetty", name: "jetty-servlet", version: "9.2.11.v20150529"

    compile group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.9.8"

    testCompile group: "junit", name: "junit", version: "4.12"

}